bin2chen

high

# getFundingFeeAmount() wrong precision

## Summary

## Vulnerability Detail
The new version of `getFundingFeeAmount()` will divide by `FLOAT_PRECISION_SQRTF` first to avoid overflows   
The code is as follows:

```solidity
    function getFundingFeeAmount(
        int256 latestFundingAmountPerSize,
        int256 positionFundingAmountPerSize,
        uint256 positionSizeInUsd
    ) internal pure returns (int256) {
        int256 fundingDiffFactor = (latestFundingAmountPerSize - positionFundingAmountPerSize);

        // divide the positionSizeInUsd by Precision.FLOAT_PRECISION_SQRT as the fundingAmountPerSize values
        // are stored based on FLOAT_PRECISION_SQRT values instead of FLOAT_PRECISION to reduce the chance
        // of overflows
        // adjustedPositionSizeInUsd will always be zero if positionSizeInUsd is less than Precision.FLOAT_PRECISION_SQRT
        // position sizes should be validated to be above a minimum amount
        // to prevent gaming by using small positions to avoid paying funding fees
@>      uint256 adjustedPositionSizeInUsd = positionSizeInUsd / Precision.FLOAT_PRECISION_SQRT;

        int256 numerator = adjustedPositionSizeInUsd.toInt256() * fundingDiffFactor;

        if (numerator == 0) { return 0; }

        // if the funding fee amount is negative, it means this amount should be claimable
        // by the user
        // round the funding fee amount down for this case
        if (numerator < 0) {
@>          return numerator / Precision.FLOAT_PRECISION.toInt256();  //<------should div FLOAT_PRECISION_SQRT
        }

        // a user could avoid paying funding fees by continually updating the position
        // before the funding fee becomes large enough to be chargeable
        // to avoid this, the funding fee amount is rounded up if it is positive
        //
        // this could lead to large additional charges if the token has a low number of decimals
        // or if the token's value is very high, so care should be taken to inform user's if that
        // is the case
@>      return Calc.roundUpMagnitudeDivision(numerator, Precision.FLOAT_PRECISION);   //<------should div FLOAT_PRECISION_SQRT
    }
```

`AdjustedPositionSizeInUsd ` has been divided by `10 ** 15`  before , and it is wrong to divide by `FLOAT_PRECISION = 10 ** 30 ` when returning
Causes the total to be divided by  `10 ** 45`
Correct is to divide by `10 ** 15` when returning.
So the total is divided by `10 ** 30`

## Impact
`getFundingFeeAmount()` The amount is calculated incorrectly.

## Code Snippet
https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/market/MarketUtils.sol#L1363-L1373
## Tool used

Manual Review

## Recommendation
```solidity
    function getFundingFeeAmount(
        int256 latestFundingAmountPerSize,
        int256 positionFundingAmountPerSize,
        uint256 positionSizeInUsd
    ) internal pure returns (int256) {
...
        if (numerator < 0) {
-           return numerator / Precision.FLOAT_PRECISION.toInt256();
+           return numerator / Precision.FLOAT_PRECISION_SQRT.toInt256();
        }

        // a user could avoid paying funding fees by continually updating the position
        // before the funding fee becomes large enough to be chargeable
        // to avoid this, the funding fee amount is rounded up if it is positive
        //
        // this could lead to large additional charges if the token has a low number of decimals
        // or if the token's value is very high, so care should be taken to inform user's if that
        // is the case
-       return Calc.roundUpMagnitudeDivision(numerator, Precision.FLOAT_PRECISION);
+       return Calc.roundUpMagnitudeDivision(numerator, Precision.FLOAT_PRECISION_SQRT);
    }
```
