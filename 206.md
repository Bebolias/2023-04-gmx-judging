bin2chen

high

# swapProfitToCollateralToken() missing cancellation poolAmountAdjustment

## Summary
`swapProfitToCollateralToken()` did not clear the temporary `poolAmountAdjustmentKey` resulting in inaccurate subsequent price impact

## Vulnerability Detail
`swapProfitToCollateralToken()` set `poolAmountDelta` before swap for  price impact of the swap be more accurately calculated.
The code is as follows:

```solidity
    function swapProfitToCollateralToken(
        PositionUtils.UpdatePositionParams memory params,
        address pnlToken,
        uint256 profitAmount,
        int256 poolAmountDelta
    ) internal returns (bool, uint256) {
....
            // adjust the pool amount by the poolAmountDelta so that the price impact of the swap will be
            // more accurately calculated
@>          params.contracts.dataStore.setInt(Keys.poolAmountAdjustmentKey(params.market.marketToken, pnlToken), poolAmountDelta);

            try params.contracts.swapHandler.swap(
                SwapUtils.SwapParams(
                    params.contracts.dataStore,
                    params.contracts.eventEmitter,
                    params.contracts.oracle,
                    Bank(payable(params.market.marketToken)),
                    params.orderKey,
                    pnlToken, // tokenIn
                    profitAmount, // amountIn
                    swapPathMarkets, // markets
                    0, // minOutputAmount
                    params.market.marketToken, // receiver
                    params.order.uiFeeReceiver(), // uiFeeReceiver
                    false // shouldUnwrapNativeToken
                )
            ) returns (address /* tokenOut */, uint256 swapOutputAmount) {
@>              return (true, swapOutputAmount);  //<------------return but don't set poolAmountAdjustmentKey==0
            } catch Error(string memory reason) {
                emit SwapUtils.SwapReverted(reason, "");
            } catch (bytes memory reasonBytes) {
                (string memory reason, /* bool hasRevertMessage */) = ErrorUtils.getRevertMessage(reasonBytes);
                emit SwapUtils.SwapReverted(reason, reasonBytes);
            }

@>          params.contracts.dataStore.setInt(Keys.poolAmountAdjustmentKey(params.market.marketToken, pnlToken), 0);
        }

```
The above code does not set `poolAmountAdjustmentKey` to 0 first when it returns normally (without catch)
which leads to inaccurate `price impact` quantity for subsequent calls or new transactions calling `getPriceImpactUsd()`, or even revert

## Impact
getPriceImpactUsd() will additionally add a temporary `poolAmountAdjustment` that is not cleared, resulting in inaccurate quantities and even revert

## Code Snippet
https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/DecreasePositionCollateralUtils.sol#L472
## Tool used

Manual Review

## Recommendation
```solidity
    function swapProfitToCollateralToken(
        PositionUtils.UpdatePositionParams memory params,
        address pnlToken,
        uint256 profitAmount,
        int256 poolAmountDelta
    ) internal returns (bool, uint256) {
....

            ) returns (address /* tokenOut */, uint256 swapOutputAmount) {
+               params.contracts.dataStore.setInt(Keys.poolAmountAdjustmentKey(params.market.marketToken, pnlToken), 0);
                return (true, swapOutputAmount);
            } catch Error(string memory reason) {
                emit SwapUtils.SwapReverted(reason, "");
            } catch (bytes memory reasonBytes) {
                (string memory reason, /* bool hasRevertMessage */) = ErrorUtils.getRevertMessage(reasonBytes);
                emit SwapUtils.SwapReverted(reason, reasonBytes);
            }

            params.contracts.dataStore.setInt(Keys.poolAmountAdjustmentKey(params.market.marketToken, pnlToken), 0);
        }
```