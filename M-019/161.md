kutugu

medium

# getPositionPnlUsd calculation has precision errors

## Summary

sizeDeltaInTokens has precision errors in calculation, which may lead to large errors in positionPnlUsd calculated.   

## Vulnerability Detail

```solidity
        if (position.sizeInUsd() == sizeDeltaUsd) {
            cache.sizeDeltaInTokens = position.sizeInTokens();
        } else {
            if (position.isLong()) {
                // @audit precision loss
                cache.sizeDeltaInTokens = Calc.roundUpDivision(position.sizeInTokens() * sizeDeltaUsd, position.sizeInUsd());
            } else {
                // @audit precision loss
                cache.sizeDeltaInTokens = position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd();
            }
        }
        // @audit precision loss
        cache.positionPnlUsd = cache.totalPositionPnl * cache.sizeDeltaInTokens.toInt256() / position.sizeInTokens().toInt256();
```

The calculation is first divided by `sizeInUsd`, resulting in loss of precision, the maximum is `sizeInUsd - 1`.     
Then multiply the result by `totalPositionPnl` to further amplify the loss.      

Let's give a specific example:
```shell
sizeInTokens = 33
sizeDeltaUsd = 33
sizeInUsd = 1000
totalPositionPnl = 100000
sizeDeltaInTokens = sizeInTokens * sizeDeltaUsd / sizeInUsd = 0
origin output: positionPnlUsd = totalPositionPnl * sizeDeltaInTokens / sizeInTokens = 0
The correct calculation would be: positionPnlUsd = totalPositionPnl * sizeDeltaUsd / sizeInUsd = 3300
```

Here is a fuzz test:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";

contract InvariantPrecisionLossTest is Test {
    PrecisionLoss internal precisionLoss;

    function setUp() public {
        precisionLoss = new PrecisionLoss();

        targetContract(address(precisionLoss));

        bytes4[] memory selectors = new bytes4[](1);
        selectors[0] = precisionLoss.positionPnlUsdLoss.selector;

        targetSelector(
            FuzzSelector({addr: address(precisionLoss), selectors: selectors})
        );
    }

    function invariant_positionPnlUsd() public {
        assertEq(precisionLoss.loss(), 0);
    }
}

contract PrecisionLoss {
    uint256 public loss;

    function positionPnlUsdLoss(
        uint256 sizeInToken,
        uint256 sizeDeltaUsd,
        uint256 sizeInUsd,
        int256 totalPositionPnl
    ) external {
        int256 originalOutput = original(
            sizeInToken,
            sizeDeltaUsd,
            sizeInUsd,
            totalPositionPnl
        );
        int256 simplifiedOutput = simplified(
            sizeInToken,
            sizeDeltaUsd,
            sizeInUsd,
            totalPositionPnl
        );

        loss = abs(simplifiedOutput - originalOutput);
        console2.log("originalOutput   : ", originalOutput);
        console2.log("simplifiedOutput : ", simplifiedOutput);
        console2.log("precisionLoss    : ", loss);
    }

    function original(
        uint256 sizeInToken,
        uint256 sizeDeltaUsd,
        uint256 sizeInUsd,
        int256 totalPositionPnl
    ) internal pure returns (int256 positionPnlUsd) {
        uint256 sizeDeltaInTokens = (sizeInToken * sizeDeltaUsd) / sizeInUsd;
        positionPnlUsd =
            (totalPositionPnl * int256(sizeDeltaInTokens)) /
            int256(sizeInToken);
    }

    function simplified(
        uint256 /* sizeInToken */,
        uint256 sizeDeltaUsd,
        uint256 sizeInUsd,
        int256 totalPositionPnl
    ) internal pure returns (int256 positionPnlUsd) {
        positionPnlUsd =
            (totalPositionPnl * int256(sizeDeltaUsd)) /
            int256(sizeInUsd);
    }

    function abs(int256 n) internal pure returns (uint256) {
        unchecked {
            return uint256(n >= 0 ? n : -n);
        }
    }
}
```

## Impact

Medium. positionPnlUsd calculation has precision errors

## Code Snippet

- https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L212-L220

## Tool used

Foundry

## Recommendation

Use the simplified code provided above
