dacian

high

# Precision loss to profit/loss calculation in PositionUtils.getPositionPnlUsd()

## Summary
Two code paths in PositionUtils.getPositionPnlUsd() can result in precision loss to returned output positionPnlUsd (the profit/loss calculation).

## Vulnerability Detail
Consider [PositionUtils.getPositionPnlUsd() L210-220](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L210-L220):

```solidity
if (position.sizeInUsd() == sizeDeltaUsd) {
	cache.sizeDeltaInTokens = position.sizeInTokens();
} else {
	if (position.isLong()) {
/*L214*/	cache.sizeDeltaInTokens = Calc.roundUpDivision(position.sizeInTokens() * sizeDeltaUsd, position.sizeInUsd());
	} else {
/*L216*/	cache.sizeDeltaInTokens = position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd();
	}
}

/*L220*/ cache.positionPnlUsd = cache.totalPositionPnl * cache.sizeDeltaInTokens.toInt256() / position.sizeInTokens().toInt256();
```

Firstly, we assume that ``position.sizeInUsd() != sizeDeltaUsd`` and hence we enter the first "else" statement.

Now either L214 or L216 will be executed to calculate ``cache.sizeDeltaInTokens``, both will involve a division. We assume [L216](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L216) as that is the simpler example.

Then [L220](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L220) is executed which multiplies ``cache.sizeDeltaInTokens`` (that has already had a division). This results in [Division Before Multiplication](https://dacian.me/precision-loss-errors#heading-division-before-multiplication) a known class of precision loss errors.

This is also apparent (as is the solution) if we manually expand L220 to show the actual operations all together:
```solidity
// original
cache.positionPnlUsd  
  = cache.totalPositionPnl * cache.sizeDeltaInTokens.toInt256() / position.sizeInTokens().toInt256();
// expanded
cache.positionPnlUsd  
  = cache.totalPositionPnl * (position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd()).toInt256() / position.sizeInTokens().toInt256();
```
## Impact
If ``position.sizeInUsd() != sizeDeltaUsd``, precision loss will always occur when calculating a position's profit/loss unless the division works out perfectly, resulting in an incorrect profit/loss being used in upstream calculations.

## Code Snippet
I've used Foundry to create an isolated test harness that shows the precision loss in action, using input values from the test runs in test/exchange/DecreaseOrder.ts and a statelezz fuzz test that generates its own inputs to maximize the precision loss. Contract file: ``src/GmxUpdatePrecisionLoss.sol``:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "forge-std/console2.sol";

contract GmxUpdatePrecisionLoss {

    // @audit simple re-implementation of GMX PositionUtils.getPositionPnlUsd() L216-220
    // https://github.com/gmx-io/gmx-synthetics/blob/a2e331f6d0a3b59aaac5ead975b206840369a723/contracts/position/PositionUtils.sol#L216-L220
    // issue: Precision loss to profit/loss calculation in PositionUtils.getPositionPnlUsd()
    function gmxOriginalGetPositionPnlUsd(uint sizeInTokens, 
                                          uint sizeInUsd, 
                                          uint sizeDeltaUsd,
                                          uint totalPositionPnl) 
      external pure returns (uint) {

        uint sizeDeltaInTokens = sizeInTokens * sizeDeltaUsd / sizeInUsd;

        return (totalPositionPnl * sizeDeltaInTokens / sizeInTokens);
    }

    function gmxSimplifiedGetPositionPnlUsd(uint , //sizeInTokens 
                                            uint sizeInUsd, 
                                            uint sizeDeltaUsd,
                                            uint totalPositionPnl) 
      external pure returns (uint) {
        //
        // @audit expanding positionPnlUsd shows Division Before Multiplicaton
        // positionPnlUsd = totalPositionPnl * sizeDeltaInTokens / sizeInTokens;
        //                = totalPositionPnl * (sizeInTokens * sizeDeltaUsd / sizeInUsd) / sizeInTokens;
        //
        // Multiplying & dividing by sizeInTokens is redundant, simplify to:
        // positionPnlUsd = totalPositionPnl * sizeDeltaUsd / sizeInUsd;  
        // This also removes the first division, solving the precision loss issue
        //
        return (totalPositionPnl * sizeDeltaUsd / sizeInUsd);
    }
}
```

Test file: ``test/GmxUpdatePrecisionLoss.t.sol``:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {GmxUpdatePrecisionLoss} from "../src/GmxUpdatePrecisionLoss.sol";

import {console2} from "forge-std/console2.sol";
import {Test} from "forge-std/Test.sol";

contract GmxUpdatePrecisionLossTest is Test {
    // real contract
    GmxUpdatePrecisionLoss        internal _underlying;

    function setUp() public {
        _underlying = new GmxUpdatePrecisionLoss();
    }

    uint public originalOutput;
    uint public simplifiedOutput;

    uint public maxPrecisionLoss;
    uint public mplSizeInTokens;
    uint public mplSizeInUsd;
    uint public mplSizeDeltaUsd;
    uint public mplTotalPositionPnl;

    function testGmxGetPositionPnlUsd() public {
        console2.log("case #1: everything works out, no precision loss");
        // values taken from project's existing unit tests Exchange.DecreaseOrder
        uint sizeInTokens     = 39920159680638730522;
        uint sizeInUsd        = 200000000000000000000000000000000000;
        uint sizeDeltaUsd     = 200000000000000000000000000000000000;
        uint totalPositionPnl = 796809574491023062776642337522;

        originalOutput   = _underlying.gmxOriginalGetPositionPnlUsd(
            sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

        simplifiedOutput = _underlying.gmxSimplifiedGetPositionPnlUsd(
            sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

        console2.log("originalOutput   : ", originalOutput);
        console2.log("simplifiedOutput : ", simplifiedOutput);

        // no precision loss here as the division works out
        assertEq(originalOutput, simplifiedOutput);
        
        console2.log("case #2: slightly modify input, now precision loss visible");
        // now change the values such that it doesn't evenly divide
        // to expose the precision loss
        sizeInTokens     = 39920159680638730522;
        sizeInUsd        = 200000000000000000000000000000000009;
        sizeDeltaUsd     = 200000000000000000000000000000000001;
        totalPositionPnl = 796809574491023062776642337525;

        originalOutput   = _underlying.gmxOriginalGetPositionPnlUsd(
            sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

        simplifiedOutput = _underlying.gmxSimplifiedGetPositionPnlUsd(
            sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

        maxPrecisionLoss = simplifiedOutput - originalOutput;

        console2.log("originalOutput   : ", originalOutput);
        console2.log("simplifiedOutput : ", simplifiedOutput);
        console2.log("precisionLoss    : ", maxPrecisionLoss);

        assert(originalOutput != simplifiedOutput);
    }

    // statelezz fust test to try & maximize precision loss. Run with -vvvv
    // to see output. Settings in foundry.toml:
    // [fuzz]
    // runs = 100000
    // max_local_rejects = 999999999                            
    // max_test_rejects = 999999999  
    function testFuzz_gmxGetPositionPnlUsd(
        uint sizeInTokens, uint sizeInUsd, uint sizeDeltaUsd, uint totalPositionPnl) public {
        // constrain inputs to their respective precision range & valid input range
        sizeInTokens     = bound(sizeInTokens,     1e18, 100000000e18);
        sizeInUsd        = bound(sizeInUsd,        1e30, 100000000e30);
        sizeDeltaUsd     = bound(sizeDeltaUsd,     1e30, 100000000e30);
        totalPositionPnl = bound(totalPositionPnl, 1e30, 100000000e30);
        
        // run both original & simplified functions
        originalOutput   
            = _underlying.gmxOriginalGetPositionPnlUsd(sizeInTokens, sizeInUsd, 
                                                       sizeDeltaUsd, totalPositionPnl);
        simplifiedOutput 
            = _underlying.gmxSimplifiedGetPositionPnlUsd(sizeInTokens, sizeInUsd, 
                                                         sizeDeltaUsd, totalPositionPnl);

        // find the difference in precision loss
        uint precisionLoss = simplifiedOutput - originalOutput;

        //
        // if this run produced greater precision loss than all previous,
        // or if the precision loss was the same AND originalOutput == 0
        // AND simplifiedOutput > 0, then save it & its inputs
        //
        // we are really interested in seeing if we can reach a state
        // where originalOutput == 0 && simplifiedOutput > 0 as this is
        // a more damaging form of precision loss
        //
        if(precisionLoss > 0) {
            if(precisionLoss > maxPrecisionLoss || 
                (precisionLoss == maxPrecisionLoss 
              && originalOutput == 0 && simplifiedOutput > 0)) {
                maxPrecisionLoss    = precisionLoss;
                mplSizeInTokens     = sizeInTokens;
                mplSizeInUsd        = sizeInUsd;
                mplSizeDeltaUsd     = sizeDeltaUsd;
                mplTotalPositionPnl = totalPositionPnl;
                
                console2.log("originalOutput      : ", originalOutput);
                console2.log("simplifiedOutput    : ", simplifiedOutput);
                console2.log("maxPrecisionLoss    : ", maxPrecisionLoss);
                console2.log("mplSizeInTokens     : ", mplSizeInTokens);
                console2.log("mplSizeInUsd        : ", mplSizeInUsd);
                console2.log("mplSizeDeltaUsd     : ", mplSizeDeltaUsd);
                console2.log("mplTotalPositionPnl : ", mplTotalPositionPnl);
            }            
        }
    }
}
```

Run with:

```solidity
forge test --match-test testGmxGetPositionPnlUsd -vvv
```

Produced output showing precision loss in the 2nd run:

```solidity
case #1: everything works out, no precision loss
originalOutput   :  796809574491023062776642337522
simplifiedOutput :  796809574491023062776642337522
case #2: slightly modify input, now precision loss visible
originalOutput   :  796809574491023062756682257683
simplifiedOutput :  796809574491023062776642337524
precisionLoss    :  19960079841
```

I also created a stateless fuzz test in the above file to maximize the precision loss, this can be run with:
```solidity
forge test --match-test testFuzz_gmxGetPositionPnlUsd -vvvv
// sample output:
originalOutput      : 21886480637487759136396642550796630
simplifiedOutput    : 21886480637487837851660733133762294
maxPrecisionLoss    : 78715264090582965664
mplSizeInTokens     : 1000000000000000001
mplSizeInUsd        : 99999999999999999999999999999999999997
mplSizeDeltaUsd     : 25180250540790790536208033624045576
mplTotalPositionPnl : 86919232999817835740063146969337941940
```

The max precision loss observed & inputs were:
```solidity
originalOutput      : 99999999999999999899999999999999999997
simplifiedOutput    : 99999999999999999999999999999999999996
maxPrecisionLoss    : 99999999999999999999
mplSizeInTokens     : 1000000000000000000
mplSizeInUsd        : 99999999999999999999999999999999999999
mplSizeDeltaUsd     : 99999999999999999999999999999999999998
mplTotalPositionPnl : 9999999999999999999999999999999999997
```

## Tool used
Weaponized Autism & [solodit.xyz](https://solodit.xyz/) to read every single precision vulnerability reported in the history of audit contests, then this [methodology](https://dacian.me/exploiting-precision-loss-via-fuzz-testing) to actually find the issue.

## Recommendation

Assuming the simpler case where L216 executed, consider the expanded version:
```solidity
cache.positionPnlUsd = cache.totalPositionPnl * (position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd()).toInt256() / position.sizeInTokens().toInt256();

// multiplying & dividing by position.sizeInTokens() cancels out, so the calculation can be reworked as:

cache.positionPnlUsd = cache.totalPositionPnl * sizeDeltaUsd.toInt256() / position.sizeInUsd().toInt256();

//This eliminates the Division Before Multiplication, eliminating the precision loss.
```
## Note For The Judges
I have found many different precision loss errors in this codebase in many different parts. In creating all the issues I have endeavored to logically group them as I found them; each issue required painstaking careful stepping through the execution paths to identify, as every issue manifested in a different way.

Hence I am hoping that all of these findings will be treated as individual findings, as each one is different to the rest and took individual work to find. There may be a temptation to group all of my precision loss findings into 1 issue and mark the rest as duplicates.

This would be very detrimental to Sponsors in the future, as it would not incentivize auditors to find more than 1 precision loss vulnerability in the codebase. I have continued searching for & finding precision errors throughout the codebase because I anticipate being rewarded for every single one, *not* being only rewarded once and having the rest marked as duplicates, as that would remove any incentive to find more than 1.

If Sponsors wish to have multiple precision loss vulnerabilities found through their codebase, they must incentivize auditors by rewarding each finding in a different part of the code or execution path as an individual finding.