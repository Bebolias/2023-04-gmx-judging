dacian

high

# Precision loss to profit/loss calculation in PositionUtils.getPositionPnlUsd()

## Summary
Two code paths in PositionUtils.getPositionPnlUsd() can result in precision loss to returned output positionPnlUsd (the profit/loss calculation).

## Vulnerability Detail
Consider [PositionUtils.getPositionPnlUsd() L210-220](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L210-L220):

```solidity
if (position.sizeInUsd() == sizeDeltaUsd) {
	cache.sizeDeltaInTokens = position.sizeInTokens();
} else {
	if (position.isLong()) {
/*L214*/	cache.sizeDeltaInTokens = Calc.roundUpDivision(position.sizeInTokens() * sizeDeltaUsd, position.sizeInUsd());
	} else {
/*L216*/	cache.sizeDeltaInTokens = position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd();
	}
}

/*L220*/ cache.positionPnlUsd = cache.totalPositionPnl * cache.sizeDeltaInTokens.toInt256() / position.sizeInTokens().toInt256();
```

Firstly, we assume that position.sizeInUsd() != sizeDeltaUsd and hence we enter the first "else" statement.

Now either L214 or L216 will be executed to calculate cache.sizeDeltaInTokens, both will involve a division. We assume [L216](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L216) as that is the simpler example.

Then [L220](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L220) is executed which multiplies cache.sizeDeltaInTokens (that has already had a division). This results in [Division Before Multiplication](https://dacian.me/precision-loss-errors#heading-division-before-multiplication) a known class of precision loss errors.

This is also apparent (as is the solution) if we manually expand L220 to show the actual operations all together:
```solidity
// original
cache.positionPnlUsd  
  = cache.totalPositionPnl * cache.sizeDeltaInTokens.toInt256() / position.sizeInTokens().toInt256();
// expanded
cache.positionPnlUsd  
  = cache.totalPositionPnl * (position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd()).toInt256() / position.sizeInTokens().toInt256();
```
## Impact
If position.sizeInUsd() != sizeDeltaUsd, precision loss will always occur when calculating a position's profit/loss unless the division works out perfectly.

## Code Snippet
I've used Foundry to create a very simple isolated test harness that shows the precision loss in action, using input values from the test runs in test/exchange/DecreaseOrder.ts. Contract functions:

```solidity
// @audit simple re-implementation of GMX PositionUtils.getPositionPnlUsd() L214-220
// https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/position/PositionUtils.sol#L214-L220
function gmxErrorGetPositionPnlUsd(
  uint sizeInTokens, uint sizeInUsd, uint sizeDeltaUsd, uint totalPositionPnl) 
  external view returns (uint) {

	uint sizeDeltaInTokens = sizeInTokens * sizeDeltaUsd / sizeInUsd;

	uint positionPnlUsd    = totalPositionPnl * sizeDeltaInTokens / sizeInTokens;
	//
	// @audit expanding positionPnlUsd shows Division Before Multiplicaton
	// positionPnlUsd = totalPositionPnl * sizeDeltaInTokens / sizeInTokens;
	//                = totalPositionPnl * (sizeInTokens * sizeDeltaUsd / sizeInUsd) / sizeInTokens;
	//
	// Multiplying & dividing by sizeInTokens is redundant, simplify to:
	// positionPnlUsd = totalPositionPnl * sizeDeltaUsd / sizeInUsd;  
	// This also removes the first division, solving the precision loss issue
	//
	return positionPnlUsd;
}

function gmxCorrectGetPositionPnlUsd(
  uint, uint sizeInUsd, uint sizeDeltaUsd, uint totalPositionPnl) 
  external view returns (uint) {
	uint positionPnlUsd    = totalPositionPnl * sizeDeltaUsd / sizeInUsd;

	return positionPnlUsd;
}
```

Test function:

```solidity
function testGmxGetPositionPnlUsd() public {
	// values taken from tests ran in test/exchange/DecreaseOrder.ts
	uint sizeInTokens     = 39920159680638730522;
	uint sizeInUsd        = 200000000000000000000000000000000000;
	uint sizeDeltaUsd     = 200000000000000000000000000000000000;
	uint totalPositionPnl = 796809574491023062776642337522;

	uint errorReturn   = vulnContract.gmxErrorGetPositionPnlUsd(
		sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

	uint correctReturn = vulnContract.gmxCorrectGetPositionPnlUsd(
		sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

	console.log("errorReturn   : ", errorReturn);
	console.log("correctReturn : ", correctReturn);

	// no precision loss here as the division works out
	assertEq(errorReturn, correctReturn);

	// now change the values such that it doesn't evenly divide
	// to expose the precision loss
	sizeInTokens     = 39920159680638730522;
	sizeInUsd        = 200000000000000000000000000000000009;
	sizeDeltaUsd     = 200000000000000000000000000000000001;
	totalPositionPnl = 796809574491023062776642337525;

	errorReturn   = vulnContract.gmxErrorGetPositionPnlUsd(
		sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

	correctReturn = vulnContract.gmxCorrectGetPositionPnlUsd(
		sizeInTokens, sizeInUsd, sizeDeltaUsd, totalPositionPnl);

	console.log("errorReturn   : ", errorReturn);
	console.log("correctReturn : ", correctReturn);

	assert(errorReturn != correctReturn);
}
```

Run with:

```solidity
forge test --match testGmxGetPositionPnlUsd -vvv
```

Produced output showing precision loss in the 2nd run:

```solidity
errorReturn   :  796809574491023062776642337522
correctReturn :  796809574491023062776642337522
errorReturn   :  796809574491023062756682257683
correctReturn :  796809574491023062776642337524
```

## Tool used
Weaponized Autism & [solodit.xyz](https://solodit.xyz/) to read every single precision vulnerability reported in the history of audit contests :-)

## Recommendation

Assuming the simpler case where L216 executed, consider the expanded version:

cache.positionPnlUsd = cache.totalPositionPnl * (position.sizeInTokens() * sizeDeltaUsd / position.sizeInUsd()).toInt256() / position.sizeInTokens().toInt256();

Multiplying & dividing by position.sizeInTokens() cancels out, so the calculation can be reworked as:

cache.positionPnlUsd = cache.totalPositionPnl * (sizeDeltaUsd / position.sizeInUsd()).toInt256();

This eliminates the Division Before Multiplication, eliminating the precision loss.

## Note For The Judges
I have found many different precision loss errors in this codebase in many different parts. In creating all the issues I have endeavored to logically group them as I found them; each issue required painstaking careful stepping through the execution paths to identify, as every issue manifested in a different way.

Hence I am hoping that all of these findings will be treated as individual findings, as each one is different to the rest and took individual work to find. There may be a temptation to group all of my precision loss findings into 1 issue and mark the rest as duplicates.

This would be very detrimental to Sponsors in the future, as it would not incentivize auditors to find more than 1 precision loss vulnerability in the codebase. I have continued searching for & finding precision errors throughout the codebase because I anticipate being rewarded for every single one, *not* being only rewarded once and having the rest marked as duplicates, as that would remove any incentive to find more than 1.

If Sponsors wish to have multiple precision loss vulnerabilities found through their codebase, they must incentivize auditors by rewarding each finding in a different part of the code or execution path as an individual finding.