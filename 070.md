chaduke

medium

# BaseOrderUtils#setExactOrderPrice() considers price plateu as ascending/descending price for limit orders. As a result, some limit orders will be executed wrongly during price plateau period.

## Summary
``BaseOrderUtils#setExactOrderPrice()`` considers price plateu as ascending/descending price for limit orders. As a result, some limit orders will be executed wrongly during price plateau period.

## Vulnerability Detail
``BaseOrderUtils#setExactOrderPrice()`` is used to set the price for increase / decrease position orders. 

[https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/order/BaseOrderUtils.sol#L203-L269](https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/order/BaseOrderUtils.sol#L203-L269)

For limit orders, it is important to recognize the trend of the price change, either ascending or descending. However, the following logic consider a price plateau as ascending/descending:

```javascript
  bool shouldValidateAscendingPrice = !isLong;

            bool ok = shouldValidateAscendingPrice ?
                (primaryPrice <= triggerPrice && triggerPrice <= secondaryPrice) :
                (primaryPrice >= triggerPrice && triggerPrice >= secondaryPrice);

            if (!ok) {
                revert Errors.InvalidStopLossOrderPrices(primaryPrice, secondaryPrice, triggerPrice, shouldValidateAscendingPrice);
            }
```

As a result, some limit orders will be executed wrongly during price plateau period.

## Impact

BaseOrderUtils#setExactOrderPrice() considers price plateu as ascending/descending price for limit orders. As a result, some limit orders will be executed wrongly during price plateau period.

## Code Snippet

## Tool used
VSCode

Manual Review

## Recommendation
Use a strict comparison to make sure it is either a strictly ascending or descending trend.

```diff
 function setExactOrderPrice(
        Oracle oracle,
        address indexToken,
        Order.OrderType orderType,
        uint256 triggerPrice,
        bool isLong
    ) internal {
        if (isSwapOrder(orderType)) {
            return;
        }

        bool isIncrease = isIncreaseOrder(orderType);
        // increase order:
        //     - long: use the larger price
        //     - short: use the smaller price
        // decrease order:
        //     - long: use the smaller price
        //     - short: use the larger price
        bool shouldUseMaxPrice = isIncrease ? isLong : !isLong;

        if (orderType == Order.OrderType.MarketIncrease ||
            orderType == Order.OrderType.MarketDecrease ||
            orderType == Order.OrderType.Liquidation) {

            Price.Props memory price = oracle.getPrimaryPrice(indexToken);

            oracle.setCustomPrice(indexToken, Price.Props(
                price.pickPrice(shouldUseMaxPrice),
                price.pickPrice(shouldUseMaxPrice)
            ));

            return;
        }

        if (orderType == Order.OrderType.LimitIncrease ||
            orderType == Order.OrderType.LimitDecrease
        ) {
            uint256 primaryPrice = oracle.getPrimaryPrice(indexToken).pickPrice(shouldUseMaxPrice);

            // for limit increase orders:
            //      - long: validate primaryPrice < triggerPrice
            //      - short: validate primaryPrice > triggerPrice
            // for limit decrease orders:
            //      - long: validate primaryPrice > triggerPrice
            //      - short: validate primaryPrice < triggerPrice
            bool shouldValidateSmallerPrimaryPrice = shouldUseMaxPrice;

            bool ok = shouldValidateSmallerPrimaryPrice ? primaryPrice <= triggerPrice : primaryPrice >= triggerPrice;

            if (!ok) {
                revert Errors.InvalidLimitOrderPrices(primaryPrice, triggerPrice, shouldValidateSmallerPrimaryPrice);
            }

            if (shouldValidateSmallerPrimaryPrice) {
                oracle.setCustomPrice(indexToken, Price.Props(
                    primaryPrice, // min price that order can be executed with
                    triggerPrice // max price that order can be executed with
                ));
            } else {
                oracle.setCustomPrice(indexToken, Price.Props(
                    triggerPrice, // min price that order can be executed with
                    primaryPrice // max price that order can be executed with
                ));
            }

            return;
        }

        if (orderType == Order.OrderType.StopLossDecrease) {
            uint256 primaryPrice = oracle.getPrimaryPrice(indexToken).pickPrice(shouldUseMaxPrice);
            uint256 secondaryPrice = oracle.getSecondaryPrice(indexToken).pickPrice(shouldUseMaxPrice);

            // for stop-loss decrease orders:
            //     - long: validate descending price
            //     - short: validate ascending price
            bool shouldValidateAscendingPrice = !isLong;

            bool ok = shouldValidateAscendingPrice ?
-                (primaryPrice <= triggerPrice && triggerPrice <= secondaryPrice) :
+                (primaryPrice < triggerPrice && triggerPrice < secondaryPrice) :
-                (primaryPrice >= triggerPrice && triggerPrice >= secondaryPrice);
+               (primaryPrice > triggerPrice && triggerPrice > secondaryPrice);


            if (!ok) {
                revert Errors.InvalidStopLossOrderPrices(primaryPrice, secondaryPrice, triggerPrice, shouldValidateAscendingPrice);
            }

            if (shouldValidateAscendingPrice) {
                oracle.setCustomPrice(indexToken, Price.Props(
                    triggerPrice, // min price that order can be executed with
                    triggerPrice // max price that order can be executed with
                ));
            } else {
                oracle.setCustomPrice(indexToken, Price.Props(
                    triggerPrice, // min price that order can be executed with
                    triggerPrice // max price that order can be executed with
                ));
            }

            return;
        }

        revert Errors.UnsupportedOrderType();
    }
```
