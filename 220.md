Madalad

medium

# Oracle.sol will return the wrong price for asset if underlying aggregator hits `minAnswer`

## Summary

`Oracle#_getPriceFeedPrice` will return the wrong price for an asset if the underlying aggregator hits `minAnswer`.

## Vulnerability Detail

Chainlink aggregators have a built in circuit breaker if the price of an asset goes outside of a predetermined price band. The result is that if an asset experiences a huge drop in value (i.e. LUNA crash) the price of the oracle will continue to return the minPrice instead of the actual price of the asset. This would allow user to continue borrowing with the asset but at the wrong price. This is exactly what happened to [Venus on BSC when LUNA imploded](https://rekt.news/venus-blizz-rekt/).

```solidity
    function _getPriceFeedPrice(DataStore dataStore, address token) internal view returns (bool, uint256) {
        address priceFeedAddress = dataStore.getAddress(Keys.priceFeedKey(token));
        if (priceFeedAddress == address(0)) {
            return (false, 0);
        }

        IPriceFeed priceFeed = IPriceFeed(priceFeedAddress);

        (
            /* uint80 roundID */,
            int256 _price,
            /* uint256 startedAt */,
            uint256 timestamp,
            /* uint80 answeredInRound */
        ) = priceFeed.latestRoundData();

        if (_price <= 0) {
            revert Errors.InvalidFeedPrice(token, _price);
        }

        uint256 heartbeatDuration = dataStore.getUint(Keys.priceFeedHeartbeatDurationKey(token));
        if (block.timestamp > timestamp && block.timestamp - timestamp > heartbeatDuration) {
            revert Errors.PriceFeedNotUpdated(token, timestamp, heartbeatDuration);
        }

        uint256 price = SafeCast.toUint256(_price);
        uint256 precision = getPriceFeedMultiplier(dataStore, token);

        uint256 adjustedPrice = price * precision / Precision.FLOAT_PRECISION;

        return (true, adjustedPrice);
    }
```

## Impact

In the event that the price of an asset crashes (e.g. LUNA), the oracle will return an incorrect price. Incorrect prices mean traders PnL calculations will be incorrect, which would lead to unfair liquidations or traders receiving more/less funds than they ought to, at the expense of other traders and the protocol.

## Code Snippet

https://github.com/gmx-io/gmx-synthetics/blob/updates/contracts/oracle/Oracle.sol#L588-L619

## Tool used

Manual Review

## Recommendation

Add a check within `Oracle#_getPriceFeedPrice` that confirms that the returned price is between the aggregators min and max price bands.

```diff
    function _getPriceFeedPrice(DataStore dataStore, address token) internal view returns (bool, uint256) {
        address priceFeedAddress = dataStore.getAddress(Keys.priceFeedKey(token));
        if (priceFeedAddress == address(0)) {
            return (false, 0);
        }

        IPriceFeed priceFeed = IPriceFeed(priceFeedAddress);

        (
            /* uint80 roundID */,
            int256 _price,
            /* uint256 startedAt */,
            uint256 timestamp,
            /* uint80 answeredInRound */
        ) = priceFeed.latestRoundData();

        if (_price <= 0) {
            revert Errors.InvalidFeedPrice(token, _price);
        }

+       int192 minPrice = IChainLinkAggregator(priceFeedAddress).minAnswer();
+       int192 maxPrice = IChainLinkAggregator(priceFeedAddress).maxAnswer();
+       require(_price > minPrice, "Lower price bound breached");
+       require(_price < maxPrice, "Upper price bound breached");

        uint256 heartbeatDuration = dataStore.getUint(Keys.priceFeedHeartbeatDurationKey(token));
        if (block.timestamp > timestamp && block.timestamp - timestamp > heartbeatDuration) {
            revert Errors.PriceFeedNotUpdated(token, timestamp, heartbeatDuration);
        }

        uint256 price = SafeCast.toUint256(_price);
        uint256 precision = getPriceFeedMultiplier(dataStore, token);

        uint256 adjustedPrice = price * precision / Precision.FLOAT_PRECISION;

        return (true, adjustedPrice);
    }
```
