Inspex

medium

# Potential Stale Price in Chainlinkâ€™s latestRoundData

## Summary

There is no implemented check, such as `require(answeredInRound >= roundID)`, to verify if the returned value from the price feed provided by Chainlink indicates stale data. This lack of a check could result in the usage of outdated prices.

## Vulnerability Detail

In the `Oracle` contract, the `_getPriceFeedPrice()` function is used to retrieve the price feed from the data feed source.

```solidity=588
function _getPriceFeedPrice(DataStore dataStore, address token) internal view returns (bool, uint256) {
    address priceFeedAddress = dataStore.getAddress(Keys.priceFeedKey(token));
    if (priceFeedAddress == address(0)) {
        return (false, 0);
    }

    IPriceFeed priceFeed = IPriceFeed(priceFeedAddress);

    (
        /* uint80 roundID */,
        int256 _price,
        /* uint256 startedAt */,
        uint256 timestamp,
        /* uint80 answeredInRound */
    ) = priceFeed.latestRoundData();

    if (_price <= 0) {
        revert Errors.InvalidFeedPrice(token, _price);
    }

    uint256 heartbeatDuration = dataStore.getUint(Keys.priceFeedHeartbeatDurationKey(token));
    if (block.timestamp > timestamp && block.timestamp - timestamp > heartbeatDuration) {
        revert Errors.PriceFeedNotUpdated(token, timestamp, heartbeatDuration);
    }

    uint256 price = SafeCast.toUint256(_price);
    uint256 precision = getPriceFeedMultiplier(dataStore, token);

    uint256 adjustedPrice = price * precision / Precision.FLOAT_PRECISION;

    return (true, adjustedPrice);
}
```

https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/oracle/Oracle.sol#L588-L619

Based on the code snippet mentioned above, there is no check implemented to verify if the returned value indicates stale data. This absence of a check could result in the usage of stale prices, as cautioned in the Chainlink documentation in the link: https://docs.chain.link/data-feeds/historical-data

> answeredInRound: The combination of aggregatorAnsweredInRound and phaseId. aggregatorAnsweredInRound: The round the answer was updated in. You can check answeredInRound against the current roundId. If answeredInRound is less than roundId, the answer is being carried over. If answeredInRound is equal to roundId, then the answer is fresh.

## Impact

Outdated prices can result in incorrect profit and loss (PnL) values for traders' positions, potentially leading to liquidations or discrepancies in the amount received or paid. This can have negative implications for both traders and liquidity pools.

## Code Snippet

https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/oracle/Oracle.sol#L596-L602

## Tool used

Manual Review

## Recommendation

We suggest checking the freshness of the price by validating the `answeredInRound` against the current `roundId`.

**Oracle.sol**
```diff
diff -u Oracle.sol b.sol
--- Oracle.sol
+++ b.sol
@@ -594,11 +594,11 @@
         IPriceFeed priceFeed = IPriceFeed(priceFeedAddress);
 
         (
-            /* uint80 roundID */,
+            uint80 roundID,
             int256 _price,
             /* uint256 startedAt */,
             uint256 timestamp,
-            /* uint80 answeredInRound */
+            uint80 answeredInRound
         ) = priceFeed.latestRoundData();
 
         if (_price <= 0) {
@@ -610,6 +610,10 @@
             revert Errors.PriceFeedNotUpdated(token, timestamp, heartbeatDuration);
         }
 
+        if (answeredInRound < roundID) {
+            revert Errors.StalePrice(token, answeredInRound, roundID);
+        }
+
         uint256 price = SafeCast.toUint256(_price);
         uint256 precision = getPriceFeedMultiplier(dataStore, token);
```

https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/oracle/Oracle.sol

**Errors.sol**
```diff
diff -u Errors.sol b.sol
--- Errors.sol  
+++ b.sol
@@ -104,6 +104,7 @@
     error EmptyPriceFeedMultiplier(address token);
     error InvalidFeedPrice(address token, int256 price);
     error PriceFeedNotUpdated(address token, uint256 timestamp, uint256 heartbeatDuration);
+    error StalePrice(address token, uint80 answeredInRound, uint80 roundID);
     error MaxSignerIndex(uint256 signerIndex, uint256 maxSignerIndex);
     error InvalidOraclePrice(address token);
     error InvalidSignerMinMaxPrice(uint256 minPrice, uint256 maxPrice);
```

https://github.com/sherlock-audit/2023-04-gmx/blob/main/gmx-synthetics/contracts/error/Errors.sol